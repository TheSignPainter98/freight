local *

import 'quicktype' as :declare_type, :F, :T
import 'spec' as :spec

declare_type 'SlotInfo', [[{
  count: number,
  displayName: string,
}]]

declare_type 'Stockpile', [[{
  info: () => StockpileInfo,
}]]
export class Stockpile
  new: F '([Inventory]) => <>', (@inventories) =>

  info: F '() => [StockpileInfo]', =>
    stockpile_by_item = {}
    for inventory in *@inventories
      inventory\gather_info stockpile_by_item

    stockpile = [ info for _, info in pairs stockpile_by_item ]
    table.sort stockpile, (a, b) ->
      a.item < b.item
    stockpile

declare_type 'Inventory', [[{
  gather_info: ({string->StoredItemInfo}) => <>,
}]]
declare_type 'InventoryBackend', [[{
  size: () => number,
  slot_content: (number) => SlotInfo,
  slot_capacity: (number) => number,
}]]
export class Inventory
  new: F '(InventoryBackend) => <>', (@backend) =>

  gather_info: F '({string->StoredItemInfo}) => <>', (stored_item_info_by_item) =>
    with stored_item_info_by_item
      for i = 1, @backend\size!
        slot = @backend\slot_content i
        if not slot?
          continue

        item = slot.displayName
        [item] ??= T 'StockpileInfo',
          :item
          stored: 0
          capacity: 0
        with [item]
          .capacity += @backend\slot_capacity i
          .stored += slot.count
    return

declare_type 'StockpileInfo', '[StoredItemInfo]'
declare_type 'StoredItemInfo', [[{
  item: string,
  stored: number,
  capacity: number,
}]]
export class MinecraftBackend
  @find: F '() => [InventoryBackend]', =>
    inventory_peripherals = { peripheral.find 'inventory' }
    [ MinecraftBackend peripheral for peripheral in *inventory_peripherals]

  new: (@inventory) =>

  size: F '() => number', =>
    @inventory\size!

  slot_content: F '(number) => number', (slot_id) =>
    @inventory\getItemDetail slot_id

  slot_capacity: F '(number) => SlotInfo', (slot_id) =>
    @inventory\getItemLimit slot_id


declare_type 'TestInventoryBackendOpts', [[{
  size: ?() => number,
  slot_content: ?(number) => SlotInfo,
  slot_capacity: ?(number) => number,
}]]
export class TestInventoryBackend
  new: F '(?TestInventoryBackendOpts) => <>', (opts={}) =>
    {
      :size=-> error 'size unimplemented'
      :slot_content=-> error 'slot_content unimplemented'
      :slot_capacity=-> error 'slot_capacity unimplemented'
    } = opts
    @size = F '() => number', size
    @slot_content = F '(number) => SlotInfo', slot_content
    @slot_capacity = F '(number) => number', slot_capacity

spec ->
  import 'spec_macros' as $

  import 'spec' as :describe, :it, :matchers

  import deep_eq, errors, matches from matchers

  describe 'Stockpile', ->
    describe '\\info', ->
      it 'returns all info', ->
        item1 = 'Diamond Axe'
        item2 = 'Enchanting Table'
        item3 = 'Fishing Rod'
        inventory1 = Inventory TestInventoryBackend
          size: => 4
          slot_content: (slot_id) =>
            displayName = if slot_id % 2 == 1
              item1
            else
              item2
            {
              count: 16 * slot_id
              :displayName
            }
          slot_capacity: (slot_id) =>
            32 * slot_id
        inventory2 = Inventory TestInventoryBackend
          size: => 4
          slot_content: (slot_id) =>
            displayName = if slot_id % 2 == 1
              item2
            else
              item3
            {
              count: 3 + 16 * slot_id
              :displayName
            }
          slot_capacity: (slot_id) =>
            3 + 32 * slot_id
        stockpile = Stockpile
          * inventory1
          * inventory2

        info = stockpile\info!
        $expect_that info, deep_eq {{
          item: item1
          stored: 16 * (1 + 3)
          capacity: 32 * (1 + 3)
        }, {
          item: item2
          stored: 16 * (1 + 2 + 3 + 4) + 3 * 2
          capacity: 32 * (1 + 2 + 3 + 4) + 3 * 2
        }, {
          item: item3
          stored: 16 * (2 + 4) + 3 * 2
          capacity: 32 * (2 + 4) + 3 * 2
        }}
