local *

import 'freight.logger' as :trace
import 'quicktype' as :declare_singleton_type, :declare_type, :F, :T
import 'spec' as :spec

export TIMEOUT = <tostring>: => "TIMEOUT"
declare_singleton_type TIMEOUT

declare_type 'UplinkBackend', [[{
  send: (number, any, ?string) => boolean,
  receive: (?string, ?number) => <?number, any, ?string>,
  broadcast: (any, ?string) => <>,
}]]

declare_type 'Uplink', [[{
  broadcast: (Packet) => <>,
  send_to: (number, Packet) => <>,
  receive_from_any: (Packet, ?UplinkReceiveOpts) => <?number, Packet|TIMEOUT>,
  receive_from: (number, Packet, ?UplinkReceiveOpts) => <?number, Packet|TIMEOUT>,
}]]
export class Uplink
  new: F '(?UplinkBackend) => <>', (@backend=MinecraftBackend!) =>
    @deaf = false

  deafen: F '() => <>', =>
    @deaf = true

  broadcast: F '(Packet) => <>', (message) =>
    @backend\broadcast message, message\protocol!

  send_to: F '(number, Packet) => <>', (to_id, message) =>
    ok = @backend\send to_id, message, message\protocol!
    if not ok
      error "failed to send #{message\protocol!}"

  receive_from_any: F '(?Packet, ?UplinkReceiveOpts) => <?number, Packet|TIMEOUT>', (message_type, opts={}) =>
    @_receive_from nil, message_type, opts

  receive_from: F '(number, ?Packet, ?UplinkReceiveOpts) => <?number, Packet|TIMEOUT>', (from_id, message_type, opts={}) =>
    @_receive_from from_id, message_type, opts

  _receive_from: F '(?number, ?Packet, ?UplinkReceiveOpts) => <?number, Packet|TIMEOUT>', (from_id, message_type, opts={}) =>
    {:timeout} = opts

    local id, message, protocol
    protocol_filter = message_type?::protocol!
    while true
      id, message, protocol = @backend\receive protocol_filter, timeout
      if @deaf
        continue

      if not id?
        return nil, TIMEOUT
      if not from_id? or id == from_id
        break

    if not message_type?
      message.<> = __index: protocol: => protocol
      return id, message

    data, ok = message_type::from message, protocol
    if not ok
      import 'spec' as :repr
      error "unexpected message #{repr message}, expected #{message_type::protocol!} but got #{message\protocol!}"
    id, data
declare_type 'UplinkReceiveOpts', [[{
  timeout: ?number,
}]]

declare_type 'PeripheralType', '"modem"'
class MinecraftBackend
  new: =>
    peripheral.find 'modem', rednet.open
    if not rednet.isOpen!
      error 'cannot create Uplink: no modem'

  send: F '(number, any, ?string) => boolean', (recipient, message, protocol) =>
    rednet.send recipient, message, protocol

  receive: F '(?string, ?number) => <?number, ?any, ?string>', (protocol, timeout) =>
    rednet.receive protocol, timeout

  broadcast: F '(any, ?string) => <>', (message, protocol) =>
    rednet.broadcast message, protocol

declare_type 'Packet', [[{
  protocol: () => string,
}]]
export class Packet
  @protocol: F '() => string', =>
    @.__class.__name

  protocol: F '() => string', => @@protocol!

  @from: F '({}, string) => <?Packet, boolean>', (raw, raw_protocol) =>
    if raw_protocol == @@protocol!
      raw.<> = __index: @
      raw, true
    else
      nil, false

declare_type 'TestUplinkBackendOpts', [[{
  to_receive: ?[NetworkFrameSpec] | () -> [NetworkFrameSpec],
  on_send: ?(number, any, ?string) => boolean,
  on_receive: ?(?string, ?number) => <?number, any|TIMEOUT, ?string>,
  on_broadcast: ?(any, ?string) => boolean,
}]]
declare_type 'NetworkFrameSpec', 'NetworkFrame | () -> NetworkFrame'
declare_type 'NetworkFrame', [[
  { from_id: number } + NetworkFrameData
]]
declare_type 'NetworkFrameData', [[{
  message: any,
  protocol: ?string,
}]]
export class TestUplinkBackend
  new: F '(?TestUplinkBackendOpts) => <>', (opts={}) =>
    with opts
      -- Forbid use of old hooks
      assert not .send, "internal test error: TestUplinkBackend.send is deprecated"
      assert not .receive, "internal test error: TestUplinkBackend.receive is deprecated"
      assert not .broadcast, "internal test error: TestUplinkBackend.broadcast is deprecated"

    {
      :to_receive={},
      :on_send,
      :on_receive,
      :on_broadcast,
    } = opts

    if on_send?
      @on_send = F '(number, any, ?string) => boolean', on_send
    if on_receive?
      @on_receive = F '(?string, ?number) => boolean', on_receive
    if on_broadcast?
      @on_broadcast = F '(any, ?string) => boolean', on_broadcast

    @num_receives = 0

    @original_to_receive = to_receive
    @reset!

  reset: F '() => <>', =>
    @to_receive = T '[NetworkFrameSpec]',
      switch type @original_to_receive
        when 'table'
          [ frame for frame in *@original_to_receive ]
        when 'function'
          @original_to_receive!
    @num_to_receive = #@to_receive
    @sent = T '[NetworkFrame]', {}
    @broadcasted = T '[NetworkFrameData]', {}

  send: F '(number, any, ?string) => boolean', (id, message, protocol) =>
    if @on_send? and not @on_send id, message, protocol
      return false
    @sent[] = :id, :message, :protocol
    true

  broadcast: F '(any, ?string) => <>', (message, protocol) =>
    if @on_broadcast? and not @on_broadcast message, protocol
      return false
    @broadcasted[] = :message, :protocol

  receive: F '(?string, ?number) => <?number, any|TIMEOUT, ?string>', (protocol_filter, timeout) =>
    if @on_receive? and not @on_receive protocol_filter, timeout
      trace "! receipt of #{protocol_filter} aborted"
      return @on_absent_frame protocol_filter, timeout

    for i = 1, @num_to_receive
      frame = @to_receive[i]
      if not frame?
        continue
      if 'function' == type frame
        frame = T 'NetworkFrame', frame!
      { :from_id, :message, :protocol } = frame
      if not protocol? and message.protocol?
        protocol = message\protocol!

      if not protocol_filter? or protocol_filter == protocol
        @to_receive[i] = nil
        @num_receives += 1
        return from_id, message, protocol

    trace "! no more #{protocol_filter? and protocol_filter .. ' ' or ''}packets to receive"
    @on_absent_frame protocol_filter, timeout

  on_absent_frame: F '(?string, ?number) => <nil, TIMEOUT, nil>', (protocol_filter, timeout) =>
    if timeout?
      return nil, TIMEOUT, nil

    if not coroutine.running!?
      error "internal test error: network-read of #{protocol_filter} would cause jam (no timeout, not in coroutine)"
    while true
      coroutine.yield!

declare_type 'IdempotenceToken', 'number'
IDEMP_TOK_MAX = bit.lshift 1, 30
export IdempotenceToken = F '() -> IdempotenceToken', ->
  math.random 1, IDEMP_TOK_MAX

spec ->
  import 'spec_macros' as $

  import 'spec' as :describe, :it, :matchers

  import deep_eq, eq, errors, matches, no_errors from matchers

  class TestPacket extends Packet
    new: (@data) =>

  class TestPacket2 extends Packet
    new: (@data) =>

  describe 'Uplink', ->
    describe 'default backend', ->
      if not _G.skip_minecraft_tests
        return

      it 'requires a modem', ->
        if peripheral.find 'modem'
          $expect_that (-> Uplink!), no_errors!
        else
          $expect_that (-> Uplink!), errors matches 'cannot find modem'

    describe '\\broadcast', ->
      it 'sends the given packet', ->
        uplink = Uplink TestUplinkBackend!

        uplink\broadcast TestPacket 'data-1'
        uplink\broadcast TestPacket 'data-2'

        $expect_that uplink.backend.broadcasted, deep_eq
          * protocol: 'TestPacket', message: TestPacket 'data-1'
          * protocol: 'TestPacket', message: TestPacket 'data-2'

    describe '\\send_to', ->
      it 'sends the given packet', ->
        uplink = Uplink TestUplinkBackend!

        uplink\send_to 1, TestPacket 'message-1'
        uplink\send_to 2, TestPacket 'message-2'

        $expect_that uplink.backend.sent, deep_eq
          * id: 1, protocol: 'TestPacket', message: TestPacket 'message-1'
          * id: 2, protocol: 'TestPacket', message: TestPacket 'message-2'

      it 'handles transmission failure', ->
        uplink = Uplink TestUplinkBackend
          on_send: (_, _, _) -> false

        $expect_that (-> uplink\send_to 1, TestPacket 'message-1'), errors matches "failed to send TestPacket"

    describe '\\receive_from_any', ->
      it 'receives the only given packet', ->
        num_received = 0
        uplink = Uplink TestUplinkBackend
          to_receive:
            * from_id: 1
              message: TestPacket 'message-1'
            * from_id: 2
              message: TestPacket2 'message-2'
            * from_id: 3
              message: TestPacket 'message-3'
            * from_id: 4
              message: TestPacket2 'message-4'

        received = with {}
          [] = { uplink\receive_from_any TestPacket2, timeout: 1 }
          [] = { uplink\receive_from_any TestPacket, timeout: 1 }
          [] = { uplink\receive_from_any TestPacket2, timeout: 1 }
          [] = { uplink\receive_from_any TestPacket, timeout: 1 }

        $expect_that received, deep_eq
          * { 2, TestPacket2 'message-2' }
          * { 1, TestPacket 'message-1' }
          * { 4, TestPacket2 'message-4' }
          * { 3, TestPacket 'message-3' }

      it 'gracefully handles timeout', ->
        uplink = Uplink TestUplinkBackend to_receive: {}
        id, err = uplink\receive_from_any TestPacket, timeout: 1
        $expect_that id, eq nil
        $expect_that err, eq TIMEOUT

    describe '\\receive_from', ->
      it 'receives the only given packet', ->
        num_received = 0
        uplink = Uplink TestUplinkBackend
          to_receive:
            * from_id: 1
              message: TestPacket 'message-1'
            * from_id: 2
              message: TestPacket2 'message-2'
            * from_id: 3
              message: TestPacket 'message-3'
            * from_id: 4
              message: TestPacket2 'message-4'
        received = with {}
          [] = { uplink\receive_from 3, TestPacket, timeout: 1 }

        $expect_that received, deep_eq
          * { 3, TestPacket 'message-3' }

  describe 'Packet', ->
    it 'survives roundtrip', ->
      original = TestPacket 'hello'

      correct_decode, ok = TestPacket::from original, TestPacket\protocol!
      $expect_that correct_decode, deep_eq {data: 'hello'}
      $expect_that ok, eq true

      incorrect_decode, ok = TestPacket2::from {protocol: ->}, TestPacket\protocol!
      $expect_that incorrect_decode, eq nil
      $expect_that ok, eq false

  describe 'IdempotenceToken', ->
    it 'is safe to cross lua machine boundaries', ->
      idemp_tok = IdempotenceToken!
      $expect_that idemp_tok.<>, eq nil
