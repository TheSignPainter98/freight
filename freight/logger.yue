local *

verbose = false

LOG_DIR = "logs"
LOG_FILE = "#{LOG_DIR}/run-#{os.date '%Y-%m-%d@%H:%M:%S'}.log"

export set_log_verbosity = (v) ->
  verbose = v

export log = (fn) ->
  msg = fn!
  if verbose
    print msg
  trace msg

export fatal = (...) ->
  fragments = with {}
    for i = 1, select '#', ...
      [] = tostring select i, ...
  msg = table.concat fragments, ' '
  print msg
  trace msg

  os.exit 1

test_mode = false
export activate_test_mode = ->
  test_mode = true

setup_done = false
export trace = (msg) ->
  if not setup_done
    setup_done = true
    setup_logs!

  if test_mode
    print msg
    return

  timestamp = os.date '%Y-%m-%d@%H:%M:%S'
  with? io.open LOG_FILE, 'a'
    \write "[#{timestamp}]: #{msg}\n"
    \close!

MAX_LOGS = 10
setup_logs = ->
  fs.makeDir LOG_DIR

  existing_logs = fs.list LOG_DIR
  table.sort existing_logs, (a, b) -> a > b
  for log_to_delete in *existing_logs[MAX_LOGS-1,]
    os.remove "#{LOG_DIR}/#{log_to_delete}"
