local *

import spec from require 'spec'
import declare_type, F, T from require 'quicktype'

UNIMPLEMENTED = <call>: (...) ->

export class StateMachineBuilder
  new: =>
    @_states = T '{string -> table}', {}
    @_initial_state_name = T '?string', nil
    @_reporter = T '?(State) -> <>', nil

  set_initial_state: F '(string) => Self', (@_initial_state_name) => @

  set_reporter: F '((State) -> <>) => Self', (@_reporter) => @

  add_state: F '(StateSpec) => Self', (state) =>
    if @_states[state._name]?
      error "cannot build state machine: state '#{state._name}' redefined"
    @_states[state._name] = state
    @

  build: F '() => StateMachine', =>
    if not @_initial_state_name?
      error 'cannot build state machine: initial state undefined'
    if not @_states[@_initial_state_name]?
      error "cannot build state machine: initial state '#{@_initial_state_name}' undefined"
    if not is_empty @_states[@_initial_state_name]._fields
      error 'cannot build state machine: initial state has parameter fields'

    index = with {}
      make_state = F '(string, {string->string}, {string->any}) -> {}', (name, arg_specs, args) ->
        args = { ...args } -- Clone for safety

        new_state = with :name, <>: {}
          for field, spec in pairs arg_specs
            [field] = T spec, args[field]
            args[field] = nil
          if field = next args
            error "no such field #{name}.#{field}"
          .<index> = (key) =>
            if arg_specs[key]?
              return nil -- Valid nil.
            error "no such field #{name}.#{key}"
        {
          <index>: new_state
          <newindex>: F '(string, any) => ?any', (key, value) =>
            arg_spec = arg_specs[key]
            if not arg_spec?
              error "no such field #{name}.#{key}"
            new_state[key] = T arg_spec, value
        }
      .state = make_state @_initial_state_name, {}, {}

      states = @_states

      reporter = @_reporter
      .goto = F '(string, ?{string -> any}) => <>', (name, args={}) =>
        if not states[@state.name]?._transitions[name]
          error "no such transition: #{@state.name} -> #{name}"
        new_state_spec = states[name]
        if not new_state_spec?
          error "internal error: no such state '#{name}'"
        .state = make_state name, new_state_spec._fields, args
        if reporter?
          reporter .state

      initial_state_name = @_initial_state_name
      .to_graphviz = F '() => string', =>
        lines = with {}
          [] = 'digraph {'
          [] = '  bgcolor = black'
          [] = '  color = white'
          [] = '  node ['
          [] = '    color = white'
          [] = '    fontcolor = white'
          [] = '    fillcolor = white'
          [] = '  ]'
          [] = '  edge ['
          [] = '    color = white'
          [] = '    fontcolor = white'
          [] = '  ]'
          [] = '  levels=1'

          [] = "  #{initial_state_name}[shape=point]"
          for state_name, state in pairs states
            [] = ''
            field_names = [ field_name for field_name in pairs state._fields ]
            table.sort field_names
            if #field_names == 0
              [] = "  #{state_name}"
            else
              [] = "  #{state_name} [label=\"#{state_name}(#{table.concat field_names, ', '})\"]"
            for other_state in pairs state._transitions
              [] = "  #{state_name} -> #{other_state}"

          [] = '}'
        table.concat lines, '\n'


    T 'StateMachine', {
      <index>: index,
      <newindex>: (key, v) =>
        error "cannot add field '#{key}' to state machine"
    }

is_empty = (table) ->
  not (next table)?

declare_type 'StateSpec', [[{
  _transitions: {string},
  _fields: {string -> string},
}]]
export class StateSpec
  new: F '(string) => <>', (@_name) =>
    if not valid_state_name @_name
      error "cannot build state machine: state name '#{@_name}' not a valid identifier"

    @_transitions = T '{string}', {}
    @_fields = T '{string -> string}', {}

  add_transition_to: F '(string) => <>', (name) =>
    if not valid_state_name name
      error "cannot build state machine: state name '#{name}' not a valid identifier"
    if @_transitions[name]? or @_fields[name]?
      error "cannot build state machine: transition #{@_name}->#{name} redefined"
    @_transitions[name] = true

  add_field: F '(string, string) => <>', (name, type_spec) =>
    if not valid_field_name name
      error "cannot build state machine: field name '#{name}' not a valid identifier"
    if @_fields[name] or @_transitions[name]?
      error "cannot build state machine: field #{@_name}.#{name} redefined"
    @_fields[name] = type_spec

declare_type 'StateMachine', [[{
  state: {name: string},
  goto: (string, ?{string -> any}) => <>,
}]]

valid_state_name = F '(string) -> boolean', (name) ->
  (name\match '^[a-z][a-z0-9-]+[a-z0-9]$')?

valid_field_name = F '(string) -> boolean', (name) ->
  (name\match '^[a-z_][a-z0-9_]*$')?

spec ->
  import 'spec_macros' as $
  import describe, it, matchers from require 'spec'
  import anything, deep_eq, eq, errors, matches, no_errors from matchers

  describe 'StateMachineBuilder', ->
    it 'requires initial state', ->
      $expect_that (-> StateMachineBuilder!\build!), errors matches "cannot build state machine: initial state undefined"

    it 'rejects invalid initial state', ->
      $expect_that (-> StateMachineBuilder!\set_initial_state('phantom')\build!), errors matches "cannot build state machine: initial state 'phantom' undefined"

    it 'rejects duplicate state names', ->
      $expect_that (-> StateMachineBuilder!
        \set_initial_state 'waiting'
        \add_state StateSpec 'waiting'
        \add_state StateSpec 'waiting'
        \build!), errors matches "cannot build state machine: state 'waiting' redefined"

    it 'rejects duplicate fields', ->
      $expect_that (-> StateMachineBuilder!
        \set_initial_state 'waiting'
        \add_state with StateSpec 'waiting'
          \add_field 'hello', 'string'
          \add_field 'hello', 'number'
        \build!), errors matches "cannot build state machine: field waiting.hello redefined"

    it 'rejects invalid state names', ->
      $expect_that (-> StateMachineBuilder!
        \set_initial_state '-invalid'
        \add_state with StateSpec '-invalid'
          \add_field '-invalid', 'string'
        \build!), errors matches "cannot build state machine: state name '%-invalid' not a valid identifier"

    it 'formats itself as graphviz', ->
      graphviz_repr = StateMachineBuilder!
        \set_initial_state 'state-1'
        \add_state with StateSpec 'state-1'
          \add_transition_to 'state-2'
        \add_state with StateSpec 'state-2'
          \add_transition_to 'state-1'
          \add_field 'hello', 'string'
          \add_field 'world', 'number'
        \build!
        \to_graphviz!
      must_contain =
        * 'digraph {'
        * 'state%-1 *%[shape=point%]'
        * 'state%-1 *%-> *state%-2'
        * 'state%-2 *%[label="state%-2%(hello, world%)"%]'
        * 'state%-2 *%-> *state%-1'
      for str in *must_contain
        $expect_that graphviz_repr, matches str

  describe 'valid_state_name', ->
    valid_idents =
      * 'hello'
      * 'world-123'
    for valid_ident in *valid_idents
      it "accepts '#{valid_ident}'", ->
        $expect_that (valid_state_name valid_ident), eq true

    invalid_idents =
      * ''
      * '1234'
      * '-qwer'
      * '.asdf'
      * 'a_b'
    for invalid_ident in *invalid_idents
      it "rejects '#{invalid_ident}'", ->
        $expect_that (valid_state_name invalid_ident), eq false

  describe 'valid_field_name', ->
    valid_idents =
      * 'hello'
      * '_world123'
    for valid_ident in *valid_idents
      it "accepts #{valid_ident}", ->
        $expect_that (valid_field_name valid_ident), eq true

    invalid_idents =
      * ''
      * '1234'
      * '-qwer'
      * '.asdf'
    for invalid_ident in *invalid_idents
      it 'rejects invalid names', ->
        $expect_that (valid_field_name invalid_ident), eq false

  describe 'is_empty', ->
    it 'returns correctly', ->
      $expect_that (is_empty {}), eq true
      $expect_that (is_empty {hello: 123}), eq false

  describe 'StateMachine', ->
    make_test_sm = ->
      StateMachineBuilder!
        \set_initial_state 'state-1'
        \add_state with StateSpec 'state-1'
          \add_transition_to 'state-2'
          \add_transition_to 'state-3'
        \add_state with StateSpec 'state-2'
          \add_transition_to 'state-1'
          \add_field 'hello', 'string'
          \add_field 'world', 'number'
          \add_field 'optional', '?string'
        \add_state StateSpec 'state-3'
        \build!

    it 'rejects new fields', ->
      test_sm = make_test_sm!
      $expect_that (-> test_sm.foo = 'bar'), errors matches [[cannot add field 'foo' to state machine]]

    describe '.state', ->
      it 'validates changes', ->
        test_sm = make_test_sm!
        test_sm\goto 'state-2',
          hello: 'asdf'
          world: 321
        $expect_that (-> test_sm.state.hello = 'hello'), no_errors!
        $expect_that (-> test_sm.state.hello = 123), errors matches 'incorrect type: expected string but got number'

      it 'rejects new fields', ->
        test_sm = make_test_sm!
        $expect_that (-> test_sm.state.foo = 'bar'), errors matches 'no such field state%-1.foo'

    describe '\\goto', ->
      it 'requires at least one argument', ->
        test_sm = make_test_sm!
        $expect_that (-> test_sm\goto!), errors anything!

      it 'correctly transitions states', ->
        test_sm = make_test_sm!
        $expect_that test_sm.state.name, eq 'state-1'

        test_sm\goto 'state-2',
          hello: 'asdf'
          world: 4321
        $expect_that test_sm.state.name, eq 'state-2'
        $expect_that test_sm.state.hello, eq 'asdf'
        $expect_that test_sm.state.world, eq 4321
        $expect_that test_sm.state.optional, eq nil

        test_sm\goto 'state-1'
        $expect_that test_sm.state.name, eq 'state-1'
        $expect_that (-> test_sm.state.hello), errors matches 'no such field state%-1.hello'
        $expect_that (-> test_sm.state.world), errors matches 'no such field state%-1.world'
        $expect_that (-> test_sm.state.optional), errors matches 'no such field state%-1.optional'

      it 'rejects invalid transitions', ->
        test_sm = make_test_sm!
        $expect_that (-> test_sm\goto 'invalid'), errors matches 'no such transition: state%-1 %-> invalid'

        test_sm\goto 'state-3'
        $expect_that (-> test_sm\goto 'state-1'), errors matches 'no such transition: state%-3 %-> state%-1'

      it 'rejects transitions with missing data', ->
        test_sm = make_test_sm!
        $expect_that (-> test_sm\goto 'state-2'), errors matches 'incorrect type: expected [sn][tu][rm][ib][ne][gr] but got nil'
        $expect_that (-> test_sm\goto 'state-2', hello: 'asdf'), errors matches 'incorrect type: expected number but got nil'
        $expect_that (-> test_sm\goto 'state-2', world: 123), errors matches 'incorrect type: expected string but got nil'

      it 'validates state fields', ->
        test_sm = make_test_sm!
        $expect_that (-> test_sm\goto 'state-2', hello: 'asdf', world: 'fdsa'), errors matches 'incorrect type: expected number but got string'

      it 'rejects extra state fields', ->
        test_sm = make_test_sm!
        $expect_that (-> test_sm\goto 'state-3', spare: 'spare'), errors matches 'no such field state%-3.spare'

  describe 'set_reporter', ->
    it 'is respected by state machines', ->
      states = {}
      test_sm = StateMachineBuilder!
        \set_initial_state 'state-1'
        \set_reporter (state) ->
          states[] = state
        \add_state with StateSpec 'state-1'
          \add_transition_to 'state-2'
        \add_state with StateSpec 'state-2'
          \add_transition_to 'state-1'
          \add_field 'hello', 'string'
          \add_field 'world', 'number'
        \build!

      data =
        hello: 'asdf'
        world: 123

      test_sm\goto 'state-2', data

      $expect_that states, deep_eq
        * { name: 'state-2', ...data }

      test_sm\goto 'state-1'
      test_sm\goto 'state-2', data
      test_sm\goto 'state-1'
      test_sm\goto 'state-2', data

      $expect_that [ state.<index> for state in *states ], deep_eq
        * { name: 'state-2', ...data }
        * { name: 'state-1' }
        * { name: 'state-2', ...data }
        * { name: 'state-1' }
        * { name: 'state-2', ...data }
